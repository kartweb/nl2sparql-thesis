CQ1. Find all studies of diabetes disease

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/> 
SELECT DISTINCT ?study_name ?purpose ?inclusion
WHERE {
    GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
         ?study a obi:study_design_execution ;
                dc:identifier ?study_name;
                ro:concretizes ?study_design.
          ?study_design  ro:has_part ?protocol .

         # Primary purpose specification
         ?protocol ro:has_part ?purposeInst .
         ?purposeInst a obi:objective_specification ;
                      cmeo:has_value ?purpose .

         # Inclusion criteria pattern (optional)
         OPTIONAL {
            ?protocol ro:has_part ?inclusionInst .
            ?inclusionInst a obi:inclusion_criterion ;
                           cmeo:has_value ?inclusion .
         }
         
         # Filter: Only include studies where the primary purpose or inclusion criteria contain "diabetes"
         FILTER( regex(lcase(str(?purpose)), 'diabetes') ||
           (BOUND(?inclusion) && regex(lcase(str(?inclusion)), 'diabetes'))
         )
    }
}


------------------****************-----------------------------
CQ2. Find all studies of coronary heart disease

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/> 
SELECT DISTINCT ?study_name ?purpose ?inclusion
WHERE {
    GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
         ?study a obi:study_design_execution ;
                dc:identifier ?study_name;
                ro:concretizes ?study_design.
          ?study_design  ro:has_part ?protocol .

         # Primary purpose specification
         ?protocol ro:has_part ?purposeInst .
         ?purposeInst a obi:objective_specification ;
                      cmeo:has_value ?purpose .

         # Inclusion criteria pattern (optional)
         OPTIONAL {
            ?protocol ro:has_part ?inclusionInst .
            ?inclusionInst a obi:inclusion_criterion ;
                           cmeo:has_value ?inclusion .
         }
         
         # Filter: Only include studies where the primary purpose or inclusion criteria contain "coronary heart disease"
         FILTER( regex(lcase(str(?purpose)), 'coronary heart disease') ||
           (BOUND(?inclusion) && regex(lcase(str(?inclusion)), 'coronary heart disease'))
         )
    }
}


------------------****************-----------------------------


CQ3. Find count of variable related to time-chf study

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/> 

SELECT  (COUNT(?data_element) AS ?var_count)
WHERE {
   GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
    	?study a obi:study_design_execution;
          		dc:identifier 'time-chf';
    			ro:concretizes ?study_design .
         ?study_design ro:has_part ?protocol .
    ?protocol ro:has_part ?study_var_design.
	 ?study_var_design a cmeo:study_design_variable_specification;
              ro:has_part ?data_element.
    
  }
}

------------------****************-----------------------------


CQ4. Find count of all drug related variables in study time-chf

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/> 

      SELECT  (COUNT(?data_element) AS ?var_count)
      WHERE {
        GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
            ?study a obi:study_design_execution;

                      dc:identifier 'time-chf';
               		ro:concretizes ?study_design .
              ?study_design ro:has_part ?protocol .
          ?protocol ro:has_part ?study_var_design.
        ?study_var_design a cmeo:study_design_variable_specification;
                    ro:has_part ?data_element.
          
        }
        GRAPH ?studyGraph {
              ?data_element a cmeo:data_element;
                obi:is_specified_input_of ?catProcessA, ?stdProcessA .
            ?catProcessA a cmeo:categorization_process ;
                          obi:has_specified_output ?cat_outputA .
              
              
              ?cat_outputA cmeo:has_value "drug_exposure".

        }
      }

------------------****************-----------------------------

CQ5: What are the protocol specifications of the randomized controlled trials?

PREFIX obi:  <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc:   <http://purl.org/dc/elements/1.1/>
PREFIX bfo:  <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT
    ?study_name
(GROUP_CONCAT(DISTINCT COALESCE(?part_uri); separator=";") AS ?all_parts_uri)
(GROUP_CONCAT(DISTINCT COALESCE(?part_value); separator=";") AS ?all_parts_value)

WHERE {
  GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
      ?study  a cmeo:randomized_controlled_trial;
      			ro:has_part   ?protocol;
              dc:identifier ?study_name.
      ?protocol a obi:protocol;
              (ro:has_part)+ ?part_uri .
      ?part_uri cmeo:has_value ?part_value.
    
   
   
  }
}
GROUP BY ?study_name

------------------****************-----------------------------

CQ6:Which studies investigated heart failure condition?

PREFIX obi:  <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc:   <http://purl.org/dc/elements/1.1/>
PREFIX bfo:  <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>
PREFIX sio: <http://semanticscience.org/ontology/sio.owl/>
PREFIX iao: <http://purl.obolibrary.org/obo/iao.owl/>

SELECT DISTINCT ?study_name ?description
WHERE { 
    GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
        ?study_design dc:identifier ?study_name;
            iao:is_about ?descriptor;
            ro:has_part ?prot .
        ?descriptor rdfs:label ?description.
        ?prot a obi:protocol;
          ro:has_part ?obj_spec.
        ?obj_spec a obi:objective_specification ;
            cmeo:has_value ?obj_val.
        ?prot ro:has_part ?ec.
        ?ec a obi:eligibility_criterion;
            ro:has_part ?inc.
        ?inc a obi:inclusion_criterion;
            ro:has_part ?spec_inc.
        ?spec_inc  cmeo:has_value ?inc_value.
    
    FILTER(contains  (?obj_val,"heart failure") || contains  (?inc_value,"heart failure"))
    
  }
}

------------------****************-----------------------------

CQ7:What are the population characteristics in different studies

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>

SELECT ?study_name (GROUP_CONCAT(DISTINCT LCASE(STR(?quality_value)); separator="; ") AS ?qualities)
(GROUP_CONCAT(DISTINCT LCASE(STR(?quality_type)); separator="; ") AS ?quality_type)
WHERE {
GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
	 ?study_design dc:identifier ?study_name ;
            ro:has_part ?prot .
        ?prot a obi:protocol ;
            ro:has_part ?ec .
        ?ec a obi:eligibility_criterion ;
            ro:is_concretized_by ?hse_uri .
        ?hse_uri ro:has_output ?population .
        ?population a obi:population ;
            ro:has_characteristic ?quality .
        OPTIONAL { ?quality rdfs:label ?characteristic_value }
        OPTIONAL { ?quality a  ?characteristic_type }
}
}
GROUP BY ?study_name
LIMIT 3

------------------****************-----------------------------

CQ8:Which studies match the PICO criteria?

PREFIX obi:  <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc:   <http://purl.org/dc/elements/1.1/>
PREFIX bfo:  <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cmeo: <https://w3id.org/CMEO/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ro: <http://purl.obolibrary.org/obo/ro.owl/>
PREFIX sio: <http://semanticscience.org/ontology/sio/v1.59/sio-release.owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?study_name 
(GROUP_CONCAT(DISTINCT CONCAT(LCASE(STR(?diagnosis_value)), " [", STR(?min_val), "-", STR(?max_value), "]"); SEPARATOR="; ") AS ?population)
(GROUP_CONCAT(DISTINCT LCASE(STR(?intervention_value)); separator="; ") AS ?interventions)
(GROUP_CONCAT(DISTINCT LCASE(STR(?comparator_value)); separator="; ") AS ?comparators)
(GROUP_CONCAT(DISTINCT LCASE(STR(?outcome_value)); separator="; ") AS ?outcomes)
WHERE {  
    GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {
        ?study_design dc:identifier ?study_name;
             ro:has_part ?prot .
        ?prot a obi:protocol; 
        # Population from inclusion criteria
        	ro:has_part ?eligibility_criterion .
        ?eligibility_criterion a obi:eligibility_criterion ;
            ro:has_part ?inclusion_criteria .
        ?inclusion_criteria a obi:inclusion_criterion ;
            ro:has_part ?inc_part.
    
    	#DIAGNOSTIC INCLUSION
    	OPTIONAL{
      	
         ?inc_part a obi:health_status_inclusion_criterion;
              cmeo:has_value ?diagnosis_value .
         FILTER(contains  (?diagnosis_value, "nyha class ii"))
      }
      #AGE INCLUSION
      OPTIONAL{
      	
         ?inc_part a obi:age_group_inclusion_criterion;
            ro:has_part ?min_val_specification, ?max_value_specification.	
          ?min_val_specification a obi:minimum_age_value_specification; 
              cmeo:has_value ?min_val.
          ?max_value_specification a obi:maximum_age_value_specification; 
              cmeo:has_value  ?max_value.
          FILTER(xsd:integer(?min_val) >= 60 && xsd:integer(?max_value) <= 85)
      }
     # Intervention
    	OPTIONAL{
      
        ?prot ro:has_part ?intervention .
        ?intervention a cmeo:intervention_specification; cmeo:has_value ?intervention_value .
        FILTER(contains  (?intervention_value,"intensified"))
      
     }
        # Comparator
        ?prot ro:has_part ?comparator .
        ?comparator a cmeo:comparator_specification ;cmeo:has_value ?comparator_value .
        FILTER(contains  (?comparator_value,"standard guided"))
        # Outcomes
     }
      OPTIONAL{
        ?prot ro:has_part ?outcome .
        ?outcome a cmeo:outcome_specification ;
                ro:has_part ?suboutcome_uri.
        ?suboutcome_uri  cmeo:has_value ?outcome_value .
        FILTER(contains  (?outcome_value,"survival"))
      
    }
}
GROUP BY ?study_name

------------------****************-----------------------------

CQ9: What studies permit data access for research on congestive heart failure, subject to ethics-approval?

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX dc:  <http://purl.org/dc/elements/1.1/>
PREFIX cmeo:<https://w3id.org/CMEO/> 
PREFIX iao: <http://purl.obolibrary.org/obo/iao.owl/>         
PREFIX duo:  <http://purl.obolibrary.org/obo/duo/>

SELECT ?study_name
WHERE {
  GRAPH <https://w3id.org/CMEO/graph/studies_metadata> {

    ?assignment a cmeo:data_use_permission_assignment ;
                iao:is_about ?x_study ;
                obi:has_specified_output ?data_policy ;
                obi:has_specified_output ?data_modifier .
    ?x_study a obi:study_design_execution ;
             dc:identifier ?study_name .
    ?data_policy a duo:data_use_permission ;
                 rdfs:label "disease specific research"^^xsd:string ;
                 iao:is_about ?disease_code .
    ?data_modifier a duo:data_use_modifier ;
                   rdfs:label "ethics approval required"^^xsd:string .

    OPTIONAL { ?disease_code rdfs:label "congestive heart failure"^^xsd:string }
    OPTIONAL { ?disease_code cmeo:has_value "42343007"^^xsd:string }
  }
}
ORDER BY ?study_name


