Q1

Find all studies of diabetes disease

PREFIX bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
PREFIX cdio: <https://w3id.org/CDIO/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX ns1: <http://purl.obolibrary.org/obo/bfo.owl#>
PREFIX obi: <http://purl.obolibrary.org/obo/obi.owl/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?study 
WHERE {
  GRAPH <https://w3id.org/CDIO/graph/studies> {
    ?study a obi:study_design_execution ;
           bfo:concretizes ?study_design .
    ?study_design bfo:has_part ?protocol .

    # Primary purpose specification
    ?protocol bfo:has_part ?study_primary_purpose_specification .
    ?study_primary_purpose_specification a cdio:study_primary_purpose_specification ;
                                         cdio:has_value ?purpose .

    # Inclusion criteria pattern (optional)
    OPTIONAL {
      ?protocol bfo:has_part ?inclusionInst .
      ?inclusionInst a cdio:inclusion_criteria ;
                    cdio:has_value ?inclusion .
    }

    # Filter: Only include studies where the primary purpose or inclusion criteria contain "diabetes"
    FILTER( regex(lcase(str(?purpose)), 'diabetes') || (BOUND(?inclusion) && regex(lcase(str(?inclusion)), 'diabetes')) )
  }
}


Q2

Find all studies of coronary heart disease

prefix bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
prefix cdio: <https://w3id.org/CDIO/>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix ns1: <http://purl.obolibrary.org/obo/bfo.owl#>
prefix obi: <http://purl.obolibrary.org/obo/obi.owl/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?study 
WHERE {
  GRAPH <https://w3id.org/CDIO/graph/studies> {
    ?study a obi:study_design_execution ;
           bfo:concretizes ?study_design .
    ?study_design bfo:has_part ?protocol .

    # Primary purpose specification
    ?protocol bfo:has_part ?study_primary_purpose_specification .
    ?study_primary_purpose_specification a cdio:study_primary_purpose_specification ;
                                         cdio:has_value ?purpose .

    # Inclusion criteria pattern (optional)
    OPTIONAL {
      ?protocol bfo:has_part ?inclusionInst .
      ?inclusionInst a cdio:inclusion_criteria ;
                     cdio:has_value ?inclusion .
    }

    FILTER( regex(lcase(str(?purpose)), 'coronary heart disease') ||
            (BOUND(?inclusion) && regex(lcase(str(?inclusion)), 'coronary heart disease')) )
  }
}



Q3

Find count of variables related to time-chf study

prefix bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
prefix cdio: <https://w3id.org/CDIO/>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix ns1: <http://purl.obolibrary.org/obo/bfo.owl#>
prefix obi: <http://purl.obolibrary.org/obo/obi.owl/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT (COUNT(?data_element) AS ?var_count)
WHERE {
    GRAPH <https://w3id.org/CDIO/graph/studies>{
         ?study a obi:study_design_execution ;
                dc:identifier 'time-chf';
                bfo:concretizes ?study_design.
         ?study_design bfo:has_part ?protocol .
         ?protocol bfo:has_part ?study_var_design.
         ?study_var_design a cdio:study_design_variable_specification;
                             ns1:has_part ?data_element.
    }
}


Q4

Find count of all drug related variables in time-chf


prefix bfo: <http://purl.obolibrary.org/obo/bfo.owl/>
prefix cdio: <https://w3id.org/CDIO/>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix ns1: <http://purl.obolibrary.org/obo/bfo.owl#>
prefix obi: <http://purl.obolibrary.org/obo/obi.owl/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT (COUNT(?data_element) AS ?var_count)
WHERE {
    GRAPH <https://w3id.org/CDIO/graph/studies>{
         ?study a obi:study_design_execution ;
                dc:identifier 'time-chf';
                bfo:concretizes ?study_design.
         ?study_design bfo:has_part ?protocol .
         ?protocol bfo:has_part ?study_var_design.
         ?study_var_design a cdio:study_design_variable_specification;
                             ns1:has_part ?data_element.
    }
          
     GRAPH <https://w3id.org/CDIO/graph/time-chf> {
        ?data_element a cdio:data_element;
          obi:is_specified_input_of ?catProcessA, ?stdProcessA .
      ?catProcessA a cdio:categorization_process ;
                    obi:has_specified_output ?cat_outputA .

        ?cat_outputA cdio:has_value "drug_exposure".

     }

}
